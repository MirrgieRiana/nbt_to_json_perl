#!/usr/bin/env perl

use strict;
use warnings;
use utf8;
use Encode;
use Data::Dumper;
use JSON qw/encode_json decode_json/;

#

sub usage(*$) {
	my $fh = shift;
	my $code = shift;
	print $fh "Usage: ".__FILE__." [OPTIONS...]\n";
	print $fh "OPTION:\n";
	print $fh "\t\t--help\n";
	print $fh "\t-p,\t--pretty\n";
	print $fh "\t\t--root-name\n";
	print $fh "\t\t--type-name\n";
	print $fh "\t\t--type-key\n";
	print $fh "\t\t--flat-compound\n";
	print $fh "\t\t--flat-root\n";
	print $fh "\t\t--flat-list\n";
	print $fh "\t\t--hex-integer\n";
	print $fh "\t-r,\t--readable\n";
	exit $code;
}

my $pretty = 0;
my $root_name = 0;
my $type_name = 0;
my $type_key = 0;
my $flat_compound = 0;
my $flat_root = 0;
my $flat_list = 0;
my $hex_integer = 0;

while ($#ARGV > -1) {

	if ($ARGV[0] eq "--help") {
		shift @ARGV;
		usage *STDOUT, 0;
		next;
	}

	if ($ARGV[0] eq "-p" || $ARGV[0] eq "--pretty") {
		shift @ARGV;
		$pretty = 1;
		next;
	}

	if ($ARGV[0] eq "--root-name") {
		shift @ARGV;
		$root_name = 1;
		next;
	}

	if ($ARGV[0] eq "--type-name") {
		shift @ARGV;
		$type_name = 1;
		next;
	}

	if ($ARGV[0] eq "--type-key") {
		shift @ARGV;
		$type_key = 1;
		next;
	}

	if ($ARGV[0] eq "--flat-compound") {
		shift @ARGV;
		$flat_compound = 1;
		next;
	}

	if ($ARGV[0] eq "--flat-root") {
		shift @ARGV;
		$flat_root = 1;
		next;
	}

	if ($ARGV[0] eq "--flat-list") {
		shift @ARGV;
		$flat_list = 1;
		next;
	}

	if ($ARGV[0] eq "--hex-integer") {
		shift @ARGV;
		$hex_integer = 1;
		next;
	}

	if ($ARGV[0] eq "-r" || $ARGV[0] eq "--readable") {
		shift @ARGV;
		$root_name = 1;
		$flat_compound = 1;
		$flat_root = 1;
		$flat_list = 1;
		next;
	}

	usage *STDERR, 1;
}

#

my %types = (
	0 => ["end", "N", 0],
	1 => ["byte", "B", \&read_byte],
	2 => ["short", "S", \&read_short],
	3 => ["int", "I", \&read_int],
	4 => ["long", "L", \&read_long],
	5 => ["float", "F", \&read_float],
	6 => ["double", "D", \&read_double],
	7 => ["byte_array", "b", \&read_byte_array],
	8 => ["string", "T", \&read_string],
	9 => ["list", "A", \&read_list],
	10 => ["compound", "C", \&read_compound],
	11 => ["int_array", "i", \&read_int_array],
	12 => ["long_array", "l", \&read_long_array],
);

sub safe($) {
	return $_[0];
}

sub read_data($) {
	my $size = shift;
	if ($size == 0) {
		return "";
	} else {

		my $buffer;
		my $result = read(STDIN, $buffer, $size);

		die "Can not read from STDIN" if $result == 0;
		die "Can not read from STDIN" if ! defined $result;

		return $buffer;
	}
}

sub read_integer_1() {
	return unpack("c", read_data(1));
}

sub read_integer_2() {
	return unpack("s>", read_data(2));
}

sub read_integer_4() {
	return unpack("l>", read_data(4));
}

sub read_byte() {
	if ($hex_integer) {
		return "0x".unpack("H*", read_data(1));
	} else {
		return unpack("c", read_data(1));
	}
}

sub read_short() {
	if ($hex_integer) {
		return "0x".unpack("H*", read_data(2));
	} else {
		return unpack("s>", read_data(2));
	}
}

sub read_int() {
	if ($hex_integer) {
		return "0x".unpack("H*", read_data(4));
	} else {
		return unpack("l>", read_data(4));
	}
}

sub read_long() {
	if ($hex_integer) {
		return "0x".unpack("H*", read_data(8));
	} else {
		return unpack("q>", read_data(8));
	}
}

sub read_float() {
	my $data = read_data(4);
	return "0x".unpack("H*", $data)."=".sprintf("%.17g", unpack("f>", $data));
}

sub read_double() {
	my $data = read_data(8);
	return "0x".unpack("H*", $data)."=".sprintf("%.17g", unpack("d>", $data));
}

sub read_byte_array() {
	my $length = read_integer_4();
	die if $length < 0;
	my @values = map {
		read_byte();
	} 0 .. $length - 1;

	return \@values;
}

sub read_string() {
	my $length = read_integer_2();
	die if $length < 0;
	my $data = read_data($length);
	return decode("utf8", $data);
}

sub read_list() {
	my $type = read_integer_1();
	my $length = read_integer_4();
	die if $length < 0;
	my @values = map {
		read_payload($type);
	} 0 .. $length - 1;

	$type = $types{$type}->[0] if $type_name;

	if ($flat_list) {
		return \@values;
	} else {
		return {
			type => $type,
			values => \@values,
		};
	}
}

sub read_compound() {
	my @entries;
	while (1) {
		my $entry = read_entry();
		if ($entry->{type} == 0) {
			last;
		}
		$entry->{key} = $types{safe($entry->{type})}->[1].$entry->{key} if $type_key;
		$entry->{type} = $types{safe($entry->{type})}->[0] if $type_name;
		push @entries, $entry;
	}
	if ($flat_compound) {
		my %hash;
		for my $entry (@entries) {
			$hash{$entry->{key}} = $entry->{value};
		}
		return \%hash;
	} else {
		return \@entries;
	}
}

sub read_entry() {
	my $type = read_integer_1();
	if ($type == 0) {
		return {
			type => $type,
		};
	}
	my $key = read_string();
	my $value = read_payload($type);

	return {
		type => $type,
		key => $key,
		value => $value,
	};
}

sub read_int_array() {
	my $length = read_integer_4();
	die if $length < 0;
	my @values = map {
		read_int();
	} 0 .. $length - 1;

	return \@values;
}

sub read_long_array() {
	my $length = read_integer_4();
	die if $length < 0;
	my @values = map {
		read_long();
	} 0 .. $length - 1;

	return \@values;
}

sub read_payload($) {
	my $type = shift;
	my $a = $types{$type};
	die "Unknown Type: $type" if ! defined $a;
	return $a->[2]();
}

sub read_root() {
	my $entry = read_entry();
	$entry->{key} = "root" if $root_name;
	$entry->{key} = $types{safe($entry->{type})}->[1].$entry->{key} if $type_key;
	$entry->{type} = $types{safe($entry->{type})}->[0] if $type_name;
	if ($flat_root) {
		return {
			$entry->{key} => $entry->{value},
		};
	} else {
		return $entry;
	}
}

#

binmode STDIN;
my $json = JSON->new->canonical->ascii;
$json = $json->indent->space_after if $pretty;
print $json->encode(read_root()), "\n";
